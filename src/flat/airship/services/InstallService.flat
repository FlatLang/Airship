package flat/airship/services

import flat/datastruct/HashMap
import flat/log/Logger
import flat/io/File
import flat/git/Git

import flat/airship/Airship
import flat/airship/PackageSource
import flat/airship/Dependency
import flat/airship/GitHubDependency
import flat/airship/LocalDependency
import flat/airship/NpmDependency
import flat/airship/InvalidCommandException
import flat/airship/InvalidFlatJsonException
import flat/airship/ExecutionFailureException
import flat/airship/FlatInstallationFailureException

class {
  let static Logger log = Logger(InstallService.class)

  public construct(
    private Airship airship,
    private DependencyService dependencyService,
    private BinaryService binaryService
  ) {}

  public async install() {
    if (airship.installCommand.values.isEmpty) {
      if (airship.linkArg.enabled) {
        if (let gitUrl = airship.getGitOriginUrl()) {
          airship.linkArg.value = "."
          installAndLink(gitUrl)
          return
        } else {
          throw InvalidCommandException("#{System.workingDirectory} is not a git repository")
        }
      }
      install("main", install: true)
    } else {
      for (package in airship.installCommand.values) {
        install(package, install: true)
      }
    }
  }

  public async install(
    String package,
    Bool: install = false,
    Bool: installDependencies = true
  ) {
    if (airship.linkArg.enabled) {
      installAndLink(package)
    } else if (package.contains(":")) {
      installDependency(Dependency.fromPackage(package), install, installDependencies)
    } else {
      installLocalPackage(package)
    }
  }

  getFlatcExecutableCommand() -> String[] {
    let runtime = airship.flatcRuntimeArg.value?.toLowerCase()?.trim() ?: "java"

    match runtime {
      "native" => {
        let nativeFileName = match System.OS_INT {
          System.WINDOWS => "flatc.exe"
          default => "flatc"
        }

        return [
          "#{Airship.flatHome}/packages/Flat/target/#{nativeFileName}",
        ]
      }
      "java" => {
        return [
          "java",
          "-jar",
          "#{Airship.flatHome}/packages/Flat/target/flatc.jar",
        ]
      }
      default => {
        throw ExecutionFailureException("Invalid flatc runtime \"#{runtime}\"")
      }
    }
  }

  public async getInstallCommand(String installationTarget, PackageSource source) -> String[] {
    let currentDependency = Dependency.fromPackage("local:#{airship.packageLocation.normalizedLocation}")

    let sourceLocations = dependencyService.getDependencySourceLocations(currentDependency, installationTarget)
    let excludeSources = dependencyService.getDependencyExclusions(currentDependency, installationTarget)
    let excludeSource = installationTarget == "test" ? null : dependencyService.getDependencySourceGlobLocation(currentDependency, "test")
    let canonicalSourceLocations = sourceLocations.mapParallel({ File(_).getCanonicalPath() }).unique()

    let cmd = getFlatcExecutableCommand()

    log.trace("Adding source locations: #{sourceLocations}")
    cmd.addAll(sourceLocations.map({ "\"#_\"" }))

    cmd.addAll([
      "-install-dir", "#{Airship.flatHome}/packages",
      "-o", "#{airship.packageLocation.normalizedLocation}/#{source.outDirectory}/#{source.outFile}"
    ])

    if (airship.target) {
      log.trace("Adding target: #{airship.target}")
      cmd.addAll(["-target", airship.target])
    }
    if (airship.targetRuntimeArg.enabled) {
      log.trace("Adding source locations: #{airship.targetRuntimeArg.value}")
      cmd.addAll(["-target-runtime", airship.targetRuntimeArg.value])
    }

    log.trace("Adding source exclusions: #{excludeSources}")
    excludeSources.forEach({ cmd.addAll(["-x", "\"#_\""]) })

    if (excludeSource) {
      cmd.addAll(["-x", "\"#excludeSource\""])
    }

    let response = dependencyService.getDependenciesForSource(installationTarget, installationTarget, source)
    log.trace("All dependencies for \"#installationTarget\": #response.dependencies")

    let topLevelDependencies = dependencyService.getDependenciesForSource(installationTarget, installationTarget, source, recursive: false)
    log.trace("Tep-level dependencies for \"#installationTarget\": #topLevelDependencies.dependencies")

    let dependencyLocations = HashMap<Dependency, Array<String>>()
    let excludeLocations = HashMap<Dependency, Array<String>>()

    response.dependencies.forEachParallel((dependency) => {
      if (!dependencyLocations.containsKey(dependency)) {
        let dependencySourceLocations = dependencyService.getDependencySourceLocations(dependency, installationTarget)

        dependencyLocations[dependency] = Array()

        for (src in dependencySourceLocations) {
           dependencyLocations[dependency].add(File(src).getCanonicalPath())
        }
      }
    })

    response.dependencies
      .filter({ _.isFlatPackage() })
      .filter({ dependencyLocations[_].none(l => canonicalSourceLocations.contains(l)) })
      .forEach((dependency) => {
        dependencyService
          .getDependencySourceLocations(dependency)
          .forEach({ cmd.addAll(["-l", "\"#_\""]) })

        dependencyService
          .getDependencyExclusions(dependency, installationTarget)
          .forEach({ cmd.addAll(["-x", "\"#_\""]) })

        let dependencyExcludeSource = dependencyService.getDependencySourceGlobLocation(dependency, "test")

        if (dependencyExcludeSource) {
          cmd.addAll(["-x", "\"#dependencyExcludeSource\""])
        }
      })

    source.defaultExports
      .filter({ !_.staticImport })
      .forEach({ cmd.addAll(["-default-import", "\"#{_.location}\""]) })

    source.defaultExports
      .filter({ _.staticImport })
      .forEach({ cmd.addAll(["-default-static-import", "\"#{_.location}\""]) })

    topLevelDependencies.dependencies
      .filter({ _.isFlatPackage() })
      .filter({ dependencyLocations[_].none(l => canonicalSourceLocations.contains(l)) })
      .forEach((dependency) => {
        let childship = dependency.airship
        let dependencySource = childship.getSourceForPackage(installationTarget, childship.package.defaultSource)

        dependencySource?.defaultExports
          .filter({ !_.staticImport })
          .forEach({ cmd.addAll(["-default-import", "\"#{_.location}\""]) })

        dependencySource?.defaultExports
          .filter({ _.staticImport })
          .forEach({ cmd.addAll(["-default-static-import", "\"#{_.location}\""]) })
      })

    if (source.mainClass) {
      cmd.addAll(["-main", source.mainClass])
    } else {
      cmd.add("-library")
    }

    return cmd
  }

  async installLocalPackage(String package) {
    log.debug("Installing local package #{airship.packageLocation.normalizedLocation} with target \"#package\"")

    let source = airship.getSourceForPackage(package, airship.parent ? airship.package.defaultSource : null)

    if (!source) {
      log.debug("No source for local package #{airship.packageLocation.normalizedLocation}. Skipping installation")
      return
    }

    let dependencies = installDependencies(package, source)
    let cmd = getInstallCommand(package, source)

    if (airship.debug) {
      Console.writeLine(cmd[0] + " " + cmd.skip(1).map({ '"' + _.trimStart('"').trimEnd('"') + '"' }).join(" "))
      return
    }

    log.debug("Executing command: #cmd")

    let response = System.execute(cmd, silent: airship.quietArg.enabled)

    if (response.exitCode != 0) {
      throw FlatInstallationFailureException("Compilation failed: exit code #{response.exitCode}")
    }

    if (source.binaries.count > 0) {
      binaryService.create(package)
    }
  }

  public async installDependency(
    Dependency dependency,
    Bool: build = true,
    Bool: install = true,
    Bool: installDependencies = true
  ) {
    if (!dependencyService.isDependencyInstalled(dependency)) {
      log.debug("Installing dependency #{dependency}")
      dependencyService.installDependency(dependency)
    } else {
      log.debug("Dependency #{dependency} already installed")
    }

    if (dependency.isFlatPackage()) {
      postInstallPackage(
        File(dependency.getLocation()),
        build,
        install,
        installDependencies
      )
    }
  }

  async installAndLink(String url) {
    let start = url.lastIndexOf('/') + 1
    let end = url.lastIndexOf('.')

    if (start > end) {
      throw InvalidFlatJsonException("Invalid repo url '#url'")
    }

    let packageName = url.substring(start, end)
    let flatPackageDir = File("#{Airship.flatHome}/packages/#{packageName}")

    flatPackageDir.symbolicLinkTo(File("#{System.workingDirectory}/#{airship.linkArg.value}"))

    postInstallPackage(flatPackageDir, install: false)
  }

  async installDependencies(
    String package,
    PackageSource source,
    Bool install = false
  ) -> Dependency[] {
    let response = dependencyService.getDependenciesForSource(package, package, source)

    log.trace("Installing dependencies for source #{source}")
    log.trace("Dependencies #{response.dependencies}")

    for (dependency in response.dependencies) {
      installDependency(
        dependency,
        build: response.dependenciesDownloaded.contains(dependency),
        install: false,
        installDependencies: false
      )
    }

    return response.dependencies
  }

  async postInstallPackage(
    File flatPackageDir,
    Bool: build = true,
    Bool: install = true,
    Bool: installDependencies = true
  ) {
    log.debug("Post-installation for package #{flatPackageDir.nativeLocation}")

    let args = [
      "-f", flatPackageDir.normalizedLocation
    ]

    airship.propagateArgs(args)

    let childship = Airship(args, airship)

    if (build && childship.package?.scripts.containsKey("build")) {
      if (childship.package.scripts.containsKey("clean")) {
        childship.runScriptService.runScript("clean")
      }

      childship.runScriptService.runScript("build")
    }

    if (childship.package?.mainSource) {
      if (installDependencies) {
        childship.installService.installDependencies(flatPackageDir.name, childship.package.defaultSource)
      }
      if (install) {
        childship.installService.install()
      }
    }
  }
}