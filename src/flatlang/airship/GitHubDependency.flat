package flatlang/airship

data class extends Dependency {
  public construct(
    visible String gitHubUrl,
    visible String branch
  ) {
    super()

    let prefix = "https://github.com/"
    let suffix = ".git"

    if (gitHubUrl.toLowerCase().startsWith(prefix)) {
      gitHubUrl = "github:" + gitHubUrl.substring(prefix.count)
    }
    if (gitHubUrl.toLowerCase().endsWith(suffix)) {
      gitHubUrl = gitHubUrl.substring(0, gitHubUrl.count - suffix.count)
    }

    this.gitHubUrl = gitHubUrl
  }

  override public getLocation() -> String {
    let fullRepoName = gitHubUrl.substring("github:".count).trim()
    let repoName = fullRepoName.substring(fullRepoName.lastIndexOf('/') + 1)
    return "#{Airship.flatHome}/packages/#{repoName}"
  }

  override public toJsonKey() -> String {
    return "\"#{gitHubUrl}\""
  }

  override public toJsonValue() -> String {
    return "\"#{branch}\""
  }

  public getGitUrl() -> String {
    let repoName = gitHubUrl.substring("github:".count).trim()
    return getGitHubUrlFromRepoName(repoName)
  }

  getGitHubUrlFromRepoName(String repoName) => "https://github.com/#{repoName}.git"

  public override toString() => "|
    {
      \"gitHubUrl\": \"#{gitHubUrl}\",
      \"branch\": \"#{branch}\"
    }
    |"

  override public clone() =>
    GitHubDependency(
      gitHubUrl,
      branch
    )
}