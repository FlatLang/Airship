package flatlang/airship

import flatlang/datastruct/HashMap

import flatlang/airship/PackageSource

abstract class {
  let Airship airship => Airship(getLocation())

  public construct(String package) {
    return if (package.toLowerCase().startsWith("github:")) {
      GitHubDependency(
        gitHubUrl: package,
        branch: "master"
      )
    } else if (package.toLowerCase().startsWith("npm:")) {
      let packageName = package.contains("@") ?
        package.substring(end: package.indexOf("@")) :
        package

      let version = package.contains("@") ?
        package.substring(package.indexOf("@") + 1) :
        "latest"

      NpmDependency(packageName, version)
    } else {
      LocalDependency(package)
    }

    throw Exception("Invalid dependency \"#package\"")
  }

  public construct(String key, String value) {
    return if (key.toLowerCase().startsWith("github:")) {
      GitHubDependency(
        gitHubUrl: key,
        branch: value
      )
    } else if (key.toLowerCase().startsWith("npm:")) {
      NpmDependency(
        packageName: key.substring("npm:".count),
        version: value
      )
    } else if (value == "local") {
      LocalDependency(key)
    }

    throw Exception("Invalid dependency {\"#key\": \"#value\"}")
  }

  public abstract getLocation() -> String
  public abstract toJsonEntry() -> String

  public getSourcesForInstallation(
    String installationTarget
  ) -> PackageSource[] {
    return Array<PackageSource>()
      :add(airship.package.mainSource)
      :add(airship.getSourceForPackage(installationTarget))
      .unique()
  }
}