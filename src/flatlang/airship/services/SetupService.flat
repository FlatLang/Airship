package flatlang/airship/services

import flat/log/Logger

import flatlang/airship/Airship
import flatlang/airship/PackageSource
import flatlang/datastruct/HashSet
import flatlang/datastruct/Pair
import flatlang/io/File
import flatlang/git/Git
import flatlang/airship/InvalidFlatJsonException
import flatlang/airship/ExecutionFailureException
import flatlang/airship/GitHubDependency
import flat/path/Path

class {
  let static Logger log = Logger(SetupService.class)

  public construct(
    private Airship airship,
    private DependencyService dependencyService,
    private UpdateService updateService
  ) {}

  public async setup() {
    let airshipDependency = GitHubDependency("github:FlatLang/Airship", "master")

    if (dependencyService.isDependencyInstalled(airshipDependency)) {
      log.info("Airship already installed. Checking for updates")
      updateService.updateGitPackage(airshipDependency, build: true)
    } else {
      log.info("Installing Airship")
      airship.installService.installGitPackage(airshipDependency)
    }

    let pathValue = File(Airship.flatHome + "/bin").nativeLocation

    if (System.OS_INT == System.WINDOWS) {
      log.info("Adding \"#{pathValue}\" to user path by modifying the HKEY_CURRENT_USER/Environment/PATH registry key")
    } else {
      let files = Path.getUserShellRunCommandsFiles().map({ _.nativeLocation })
      log.info("Adding \"#{pathValue}\" to user path by modifying files: #{files.join(", ")}")
    }
    Path.appendToUserPath(pathValue)

    log.info("Finished setup!")
  }
}