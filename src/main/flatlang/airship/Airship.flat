package "flatlang/airship"

import "flatlang/datastruct/HashMap"
import "flatlang/json/JsonDeserializer"
import "flatlang/io/File"
import "flatlang/io/FileReader"

class {
  visible Package package
  visible String flatHome
  visible String outDirectory
  visible String outFile
  visible String source
  visible Bool debug

  public static async main(String[] args) {
    if (args.count < 2) {
      throw new Exception("Missing command")
    }

    let airship = await new Airship(args.skip(1)):run()
  }

  public construct(private String[] commands) {
    let packageFile = new File(getFlatJsonPath())
    let map = new JsonDeserializer().parse(packageFile)

    package = new Package((HashMap)map)
    flatHome = System.getEnv("FLAT_HOME")
    outDirectory = validateOutDirectory(package.outDirectory ?: "dist")
    outFile = package.outFile ?: "index.js"
    source = package.source ?: "src"

    debug = commands.any({ _ == "--debug"})
  }

  getFlatJsonPath() -> String {
    let fileOverride = commands.firstWhere((command, i) => {
      if (command.equalsIgnoreCase("-f") && i == commands.count - 1) {
        throw new InvalidFlatJsonException("Invalid -f value")
      }

      return i > 0 && commands[i - 1].equalsIgnoreCase("-f")
    })

    return fileOverride ?: "#{System.workingDirectory}/flat.json"
  }

  validateOutDirectory(String location) => location {
    if (location.trim().count == 0) {
      throw new InvalidOutDirException("outDirectory cannot be empty")
    } else if (location.trim().startsWith(/[/\\.]/)) {
      throw new InvalidOutDirException("outDirectory cannot start with #location.first")
    } else if (location.contains(":/") || location.contains(":\\")) {
      throw new InvalidOutDirException("outDirectory cannot be absolute")
    }
  }

  async run() {
    let commandIterator = commands.iterator

    while (commandIterator.hasNext) {
      let command = commandIterator.stepNext

      match (command.toLowerCase()) {
        "install" => await install()
        "clean" => await clean()
      }
    }
  }

  public getInstallCommand() -> String[] {
    let cmd = [
      "java",
      "-jar",
      "#{flatHome}/Flat/target/flatc.jar",
      source
    ]

    package.localDependencies.forEach({ cmd.addAll(["-l", _]) })

    cmd.addAll([
      "-install-dir", flatHome,
      "-main", package.mainClass,
      "-o", "#{outDirectory}/#{outFile}",
      "-target", "js"
    ])

    return cmd
  }

  public async install() {
    let cmd = getInstallCommand()

    if (debug) {
      Console.writeLine(cmd[0] + " " + cmd.skip(1).map({ "\"#{_}\"" }).join(" "))
      return
    }

    await System.execute(cmd)
  }

  public async clean() {
    if (!await new File(outDirectory).delete()) {
      throw new Exception("Failed to clean directory '#outDirectory'")
    }
  }
}