package "flatlang/airship/services"

import "flat/log/Logger"

import "flatlang/io/File"
import "flatlang/airship/Airship"
import "flatlang/airship/InvalidCommandException"

class {
  let static Logger log = Logger(UninstallService.class)

  public construct(private Airship airship) {}

  public async uninstall() {
    if (airship.uninstallCommand.values.isEmpty) {
      if (airship.linkArg.enabled) {
        if (let gitUrl = airship.getGitOriginUrl()) {
          airship.linkArg.value = "."
          uninstallGitPackage(gitUrl)
          return
        } else {
          throw InvalidCommandException("#{System.workingDirectory} is not a git repository")
        }
      }

      throw InvalidCommandException("Invalid uninstall command without a value")
    }

    for (package in airship.uninstallCommand.values) {
      uninstall(package)
    }
  }

  public async uninstall(String package) {
    if (package.endsWith(".git")) {
      uninstallGitPackage(package)
    } else {
      throw InvalidCommandException("Cannot uninstall package '#package'")
    }
  }

  public async uninstallGitPackage(String url) {
    let packageName = url.substring(url.lastIndexOf('/') + 1, url.lastIndexOf('.'))
    let flatPackageDir = File("#{airship.flatHome}/packages/#{packageName}")

    if (!flatPackageDir.isDirectory && !flatPackageDir.isSymbolicLink) {
      throw InvalidCommandException("Package '#packageName' is not installed")
    }

    flatPackageDir.delete(recursive: true)
  }
}