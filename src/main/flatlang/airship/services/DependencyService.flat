package flatlang/airship/services

import flat/log/Logger

import flatlang/airship/Airship
import flatlang/airship/PackageSource
import flatlang/datastruct/HashSet
import flatlang/io/File
import flatlang/git/Git
import flatlang/airship/InvalidFlatJsonException
import flatlang/airship/ExecutionFailureException
import flatlang/airship/Dependency
import flatlang/airship/GitHubDependency
import flatlang/airship/LocalDependency

import static flat/colorizer/Colorizer

class {
  let static Logger log = Logger(DependencyService.class)

  public construct(
    private Airship airship
  ) {}

  getPackageNameFromGitUrl(String url) => url.substring(url.lastIndexOf('/') + 1, url.lastIndexOf('.'))

  public getDependencyPackageLocation(Dependency dependency) => File(dependency.getLocation())

  public getDependencyLocation(Dependency dependency) -> String {
    return dependency.getLocation()
  }

  public getDependencySourceLocation(Dependency dependency) -> String {
    let src = if (let source = dependency.airship.mainSource) {
      source.source
    } else {
      "src"
    }

    return getDependencyLocation(dependency) + "/" + src
  }

  public getDependencyGitUrl(Dependency dependency) -> String {
    return if (dependency.class.isOfType(GitHubDependency.class)) {
      let gitHubDependency = (GitHubDependency)dependency

      gitHubDependency.getGitUrl()
    } else {
      throw InvalidFlatJsonException("Invalid dependency #dependency")
    }
  }

  public class DependenciesResponse {
    construct(
      visible Array<Dependency> dependencies = Array(),
      visible Array<Dependency> dependenciesDownloaded = Array()
    ) {}
  }

  public async getDependenciesForSource(
    String package,
    String installationTarget,
    PackageSource source,
    Bool: download = true,
    Bool: recursive = true
  ) -> DependenciesResponse {
    return getDependenciesForSource(
      package,
      installationTarget,
      source,
      download,
      recursive,
      DependenciesResponse(),
      HashSet()
    )
  }

  async getDependenciesForSource(
    String package,
    String installationTarget,
    PackageSource source,
    Bool download = true,
    Bool recursive = true,
    DependenciesResponse response,
    HashSet<String> dependenciesAdded
  ) -> DependenciesResponse {
    if (dependenciesAdded.contains(package)) {
      return response
    }

    dependenciesAdded.add(package)

    if (!source?.dependencies) {
      return response
    }

    for (dependency in source.dependencies) {
      if (response.dependencies.none({ getDependencyLocation(_) == getDependencyLocation(dependency) })) {
        response.dependencies.add(dependency)
      }

      let dependencyPackageFile = File(getDependencyLocation(dependency))

      if (!dependencyPackageFile.exists) {
        if (download) {
          downloadDependency(dependency)
          response.dependenciesDownloaded.add(dependency)
        }

        if (!dependencyPackageFile.exists) {
          continue
        }
      }

      let args = [
        "-f", dependencyPackageFile.normalizedLocation
      ]

      airship.propagateArgs(args)

      let childship = Airship(args, airship)

      let packageSource = match (installationTarget) {
        "main" => childship.mainSource
        "test" => childship.testSource ?: childship.mainSource
      }

      if (packageSource && recursive) {
        getDependenciesForSource(
          dependencyPackageFile.name,
          installationTarget,
          packageSource,
          download,
          recursive,
          response,
          dependenciesAdded
        )
      }
    }

    return response
  }

  public isDependencyInstalled(Dependency dependency) =>
    getDependencyPackageLocation(dependency).getChild(".git").isDirectory

  public async downloadDependency(Dependency dependency) {
    if (dependency.class.isOfType(GitHubDependency.class)) {
      downloadGitHubDependency((GitHubDependency)dependency)
    } else {
      throw Exception("Cannot download dependency #dependency")
    }
  }

  public async downloadGitHubDependency(GitHubDependency dependency) {
    let url = dependency.gitHubUrl
    let branch = dependency.branch
    let flatPackageDir = getDependencyPackageLocation(dependency):mkdirs()

    if (flatPackageDir.getChild(".git").isDirectory) {
      return
    }

    log.info("Cloning dependency #url (#branch) into '#{flatPackageDir.normalizedLocation}'...")

    let response = Git.clone(
      url,
      depth: 1,
      branch,
      outputDirectory: flatPackageDir.normalizedLocation,
      silent: true
    )

    if (response.exitCode != 0) {
      throw ExecutionFailureException("Failed to install git repo: #{red(response.stderr.join("\n"))}")
    }
  }
}